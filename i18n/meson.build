# List of translation modules and their translations
# Translatiobs are stored in i18n/l10n-<lang>_<COUNTRY>/<module>.cpp
l10n_cpp_translations = {
	'common': [
		'fr',
	]
}

i18n_incs = include_directories('.')
i18n_deps = {}
# Process l10n_cpp_translations and generate the hpp files and dependencies
po_hpp_gen = find_program('po-hpp-gen.py', native: true)
po_cpp_gen = find_program('po-cpp-gen.py', native: true)
foreach module, locales: l10n_cpp_translations
	a_i18n_module = 'arcollect-i18n-'+module
	language_switch_bodies = {}
	locales_apply_declarations = 'void apply_C(void) noexcept;\n\t\t\tvoid po_apply_C(void) noexcept;\n\t\t\t'
	cpp_files = ['l10n-C/'+module+'.cpp']
	cpp_files += custom_target('i18n-'+module+'-apply_C-po-cpp', input: 'l10n-C/'+module+'.po', output: a_i18n_module+'-apply_C.cpp', command: [po_cpp_gen,module,'C','msgid','@INPUT@','@OUTPUT@'])
	translations = []
	# Inspect locales
	foreach locale: locales
		lang = locale[0]+locale[1]
		# Handle language_switch
		language_switch = language_switch_bodies.get(lang,'')
		if locale == lang
			language_case = 'default'
		else
			country = locale[3]+locale[4]
			language_case = 'case Code("'+country+'")'
		endif
		language_switch_bodies += {lang: language_switch + '\n\t\t\t'+language_case+': apply_'+locale+'(); po_apply_'+locale+'(); break;'}
		locales_apply_declarations += 'void apply_'+locale+'(void) noexcept;\n\t\t\tvoid po_apply_'+locale+'(void) noexcept;\n\t\t\t'
		cpp_files += 'l10n-'+locale+'/'+module+'.cpp'
		cpp_files += custom_target('i18n-'+module+'-apply_'+locale+'-po-cpp', input: 'l10n-'+locale+'/'+module+'.po', output: a_i18n_module+'-apply_'+locale+'.cpp', command: [po_cpp_gen,module,locale,'msgstr','@INPUT@','@OUTPUT@'])
	endforeach
	# Generate language_switch_body
	language_switch_body = ''
	foreach lang_code, body: language_switch_bodies
		language_switch_body += '\tcase Code("'+lang_code+'"): switch(country) {'
		language_switch_body += body
		language_switch_body += '\n\t\t} break;'
	endforeach
	# Output files
	conf = configuration_data()
	conf.set('module',module)
	conf.set('locales_apply_declarations',locales_apply_declarations)
	conf.set('language_switch_body',language_switch_body)
	conf.set('translations_length',locales.length())
	conf.set('translations_content','Lang("'+'"),Lang("'.join(locales)+'")')
	
	configure_file(input: 'arcollect-i18n-struct-autogenerated.hpp', output: a_i18n_module+'-struct-autogenerated.hpp', configuration: conf)
	cpp_files += custom_target('i18n-'+module+'-po-hpp', input: 'l10n-C/'+module+'.po', output: a_i18n_module+'-po-entries-autogenerated.hpp', command: [po_hpp_gen,'@INPUT@','@OUTPUT@'])
	cpp_files += configure_file(input: 'module-template.cpp', output: 'l10n-'+module+'.cpp', configuration: conf)
	i18n_deps += { module: declare_dependency(
		link_with: static_library('l10n-'+module, cpp_files, include_directories: i18n_incs),
		include_directories: i18n_incs
	)}
endforeach
